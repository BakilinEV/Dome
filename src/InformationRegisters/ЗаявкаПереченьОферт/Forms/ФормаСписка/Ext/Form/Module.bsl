
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений;
	ПериодичностьРегистра = Метаданные.РегистрыСведений.ЗаявкаПереченьОферт.ПериодичностьРегистраСведений;
	Если ПериодичностьРегистра = Периодичность.Квартал Тогда
		КоличествоПериодов = 4;
		ФорматПериода = "ДФ='к ""Квартал""'";
	ИначеЕсли ПериодичностьРегистра = Периодичность.Месяц Тогда
		КоличествоПериодов = 12;
		ФорматПериода = "ДФ=ММММ";
	КонецЕсли;
	
	Год = Год(ТекущаяДата());
	
	Для НомерПериода=1 По КоличествоПериодов Цикл
		НоваяКнопка = Элементы.Добавить("ВыборПериода"+XMLСтрока(НомерПериода), Тип("КнопкаФормы"), Элементы.ГруппаПериод);
		НоваяКнопка.Заголовок  = Формат(Дата(Год, НомерПериода*(12/КоличествоПериодов), 1), ФорматПериода);
		НоваяКнопка.ИмяКоманды = "ВыборПериода";
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачалоПериода(Знач НаДату=Неопределено)
	
	Возврат РегистрыСведений.ЗаявкаПереченьОферт.НачалоПериода(НаДату);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьКнопки(НаДату, Знач НомерПериода=0)
	
	ДатаПериода   = НачалоПериода(НаДату);
	ДатаОкончания = КонецМесяца(НаДату);
	//НомерПериода  = Месяц(ДатаОкончания)/3;
	
	Для й=1 По КоличествоПериодов Цикл
		Элементы["ВыборПериода"+й].ЦветФона = ?(й=НомерПериода, WebЦвета.НавахоБелый, Новый Цвет);
	КонецЦикла;
	
	Элементы.Список.Период.ДатаНачала    = ДатаПериода;
	Элементы.Список.Период.ДатаОкончания = ДатаОкончания;
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	НомерПериода = Число(Сред(Этаформа.ТекущийЭлемент.Имя, 13));
	ОбновитьКнопки(Дата(Год(ДатаПериода), НомерПериода*(12/КоличествоПериодов), 1), НомерПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбновитьКнопки(ДобавитьМесяц(ДатаПериода, 12*Направление));
	
КонецПроцедуры

&НаСервере
Функция СинхронизироватьНаСервере()
	
	Возврат РегистрыСведений.ЗаявкаПереченьОферт.ЗагрузитьДанные(ДатаПериода);
	
КонецФункции

&НаКлиенте
Процедура Синхронизировать(Команда)
	
	Ошибки = СинхронизироватьНаСервере();
	
	Если ПустаяСтрока(Ошибки) Тогда
		Элементы.Список.Обновить();
		ПоказатьПредупреждение(, "Выполнено", 2);
	Иначе	
		ПоказатьПредупреждение(, Ошибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКнопки(НачалоПериода());
	
КонецПроцедуры
