
&НаКлиенте
Процедура ОбновитьКнопкиКвартала(НаДату)
	
	ДатаПериода   = НачалоКвартала(НаДату);
	ДатаОкончания = КонецКвартала(ДатаПериода);
	НомерКвартала = Месяц(ДатаОкончания)/3;
	
	Для й=1 по 4 Цикл
		Элементы["ВыборКвартала"+й].ЦветФона = ?(й=НомерКвартала, WebЦвета.НавахоБелый, Новый Цвет);
	КонецЦикла;
	
	Элементы.Список.Период.ДатаНачала    = ДатаПериода;
	Элементы.Список.Период.ДатаОкончания = ДатаОкончания;
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКвартала(Команда)
	
	НомерКвартала = Число(Прав(Этаформа.ТекущийЭлемент.Имя, 1));
	ОбновитьКнопкиКвартала(Дата(Год(ДатаПериода), НомерКвартала*3, 1));
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПериодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбновитьКнопкиКвартала(ДобавитьМесяц(ДатаПериода, 12*Направление));
	
КонецПроцедуры

&НаСервере
Функция СинхронизироватьНаСервере()
	
	Возврат РегистрыСведений.ЗаявкаПереченьОферт.ЗагрузитьДанные(ДатаПериода);
	
КонецФункции

&НаКлиенте
Процедура Синхронизировать(Команда)
	
	Ошибки = СинхронизироватьНаСервере();
	
	Если ПустаяСтрока(Ошибки) Тогда
		Элементы.Список.Обновить();
		ПоказатьПредупреждение(, "Выполнено", 2);
	Иначе	
		ПоказатьПредупреждение(, Ошибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКнопкиКвартала(НачалоКвартала(ТекущаяДата()));
	
КонецПроцедуры
