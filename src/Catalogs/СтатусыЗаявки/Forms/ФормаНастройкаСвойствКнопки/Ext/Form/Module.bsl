
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Имя = ?(Реквизит.Имя="ЗаголовокКнопки", "Заголовок", Реквизит.Имя);
		Если Параметры.Свойство(Имя) Тогда
			Параметры.Свойство(Имя, ЭтаФорма[Реквизит.Имя]);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Элементы.КнопкаОбразец, Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОформление()
	
	ЗаполнитьЗначенияСвойств(Элементы.КнопкаОбразец, РеквизитыВСтруктуру());
	
КонецПроцедуры

&НаКлиенте
Процедура ОформлениеПриИзменении(Элемент)
	
	УстановитьОформление();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСистемноеЗначение(Имя)
	
	Возврат ?(Лев(Имя, 4)="Цвет", ЦветаСтиля[Имя+"Кнопки"], ШрифтыСтиля.ОбычныйШрифтТекста);	
	
КонецФункции

&НаКлиенте
Процедура ОформлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтаФорма[Элемент.Имя] = ПолучитьСистемноеЗначение(Элемент.Имя);
	
	УстановитьОформление();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокКартинок()
	
	Список = Новый СписокЗначений;
	Список.Добавить(Новый Картинка, "< ОЧИСТИТЬ КАРТИНКУ >");
	Для Каждого ОбщаяКартинка Из Метаданные.ОбщиеКартинки Цикл
		Имя = ОбщаяКартинка.Имя;
	    Список.Добавить(БиблиотекаКартинок[Имя], ОбщаяКартинка.Синоним,, БиблиотекаКартинок[Имя]);
	КонецЦикла;
	
	Возврат Список;
		
КонецФункции

&НаКлиенте
Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеВыбора = Новый ОписаниеОповещения("КартинкаНажатиеЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, ПолучитьСписокКартинок(), Элементы.Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаНажатиеЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент <> Неопределено Тогда
		Картинка = ВыбранныйЭлемент.Значение;
		УстановитьОформление();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РеквизитыВСтруктуру()
	
	Реквизиты = Новый Структура;
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Реквизиты.Вставить(?(Реквизит.Имя="ЗаголовокКнопки", "Заголовок", Реквизит.Имя), ЭтаФорма[Реквизит.Имя]);
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

&НаКлиенте
Процедура КомандаПрименить(Команда)
	
	ОповеститьОВыборе(РеквизитыВСтруктуру());
	
КонецПроцедуры
