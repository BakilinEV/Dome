
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Выборка = Справочники.СтатусыЗаявки.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОформления = Выборка.Оформление.Получить();
		Если ПараметрыОформления = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Наименование");	
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,	"Список.Ссылка", ВидСравненияКомпоновкиДанных.Равно, Выборка.Ссылка);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ПараметрыОформления.Шрифт);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ПараметрыОформления.ЦветФона);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ПараметрыОформления.ЦветТекста);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НужноИнициализировать()
	
	Возврат Не Справочники.СтатусыЗаявки.ЕстьЭтапыЗаявки();
	
КонецФункции

&НаСервереБезКонтекста
Функция Инициализировать()
	
	Возврат Справочники.СтатусыЗаявки.ИнициализироватьБазовыеЭтапы();
	
КонецФункции

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НужноИнициализировать() Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьСписокПередНачаломДобавления", ЭтотОбъект), "Инициализировать базовые этапы?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&Наклиенте
Процедура ПродолжитьСписокПередНачаломДобавления(Знач Результат, Знач ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Инициализировать() Тогда
		Элементы.Список.Обновить();
		ПоказатьПредупреждение(, "Базовые этапы созданы");
	КонецЕсли;
	
КонецПроцедуры
