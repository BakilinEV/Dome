
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КомандаНачатьОбработкуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КомандаНачатьОбработкуНаСервере(ТолькоЗаполнитьДляСогласования=Истина)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаявкаСтроки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЗаявкаСтроки.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ЗаявкаЦены.Количество, ЗаявкаСтроки.Количество) КАК Количество,
	|	ЗаявкаСтроки.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗаявкаСтроки.НомерЗаявки КАК НомерЗаявки,
	|	ЕСТЬNULL(ЗаявкаЦены.Цена, ""-"") КАК Цена,
	|	ЕСТЬNULL(ЗаявкаЦены.Цена * ЕСТЬNULL(ЗаявкаЦены.Количество, ЗаявкаСтроки.Количество), ""-"") КАК Сумма,
	|	&Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ЗаявкаСтатусы.СрезПоследних КАК ЗаявкаСтатусы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкаСтроки КАК ЗаявкаСтроки
	|		ПО ЗаявкаСтатусы.ИдентификаторСтроки = ЗаявкаСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкаЦены.СрезПоследних КАК ЗаявкаЦены
	|		ПО ЗаявкаСтатусы.ИдентификаторСтроки = ЗаявкаЦены.ИдентификаторСтроки
	|ГДЕ
	|	ЗаявкаСтатусы.Статус.Согласование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаСтроки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЗаявкаСтроки.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ЗаявкаЦены.Количество, ЗаявкаСтроки.Количество) КАК Количество,
	|	ЗаявкаСтроки.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗаявкаСтроки.НомерЗаявки КАК НомерЗаявки,
	|	ЕСТЬNULL(ЗаявкаЦены.Цена, ""-"") КАК Цена,
	|	ЕСТЬNULL(ЗаявкаЦены.Цена, 0) * ЕСТЬNULL(ЗаявкаЦены.Количество, ЗаявкаСтроки.Количество) КАК Сумма,
	|	ЕСТЬNULL(ЗаявкаЦены.Перечень, ЗаявкаСтроки.Перечень) КАК Перечень,
	|	&Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ЗаявкаСтатусы.СрезПоследних КАК ЗаявкаСтатусы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкаСтроки КАК ЗаявкаСтроки
	|		ПО ЗаявкаСтатусы.ИдентификаторСтроки = ЗаявкаСтроки.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкаЦены.СрезПоследних КАК ЗаявкаЦены
	|		ПО ЗаявкаСтатусы.ИдентификаторСтроки = ЗаявкаЦены.ИдентификаторСтроки
	|ГДЕ
	|	(ЗаявкаСтатусы.Статус.Следующий.Согласование
	|			ИЛИ ЗаявкаСтатусы.Статус.Следующий1.Согласование
	|			ИЛИ ЗаявкаСтатусы.Статус.Следующий2.Согласование
	|			ИЛИ ЗаявкаСтатусы.Статус.Следующий3.Согласование)"
	);
	
	Запрос.УстановитьПараметр("Комментарий", ШаблонКомментария);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтрокиДляСогласования.Загрузить(РезультатЗапроса[0].Выгрузить());
	
	Если ТолькоЗаполнитьДляСогласования Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из РезультатЗапроса[1].Выгрузить() Цикл
		
		ЗаполнитьЗначенияСвойств(СтрокиДляСогласования.Добавить(), СтрокаТаблицы);
		
		РегистрыСведений.ЗаявкаСтатусы.УстановитьСледующийСтатус(СтрокаТаблицы.ИдентификаторСтроки, СтрокаТаблицы.Перечень);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНачатьОбработку(Команда)
	
	КомандаНачатьОбработкуНаСервере(Ложь);
	Элементы.СтрокиНеобработанные.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьФлажки(Команда)
	
	Для Каждого СтрокаЗаявки Из СтрокиДляСогласования Цикл
		СтрокаЗаявки.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСнятьФлажки(Команда)
	
	Для Каждого СтрокаЗаявки Из СтрокиДляСогласования Цикл
		СтрокаЗаявки.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура КомандаСогласоватьНаСервере()
	
	ПараметрыОтбора = Новый Структура("Выбран", Истина);
	НайденныеСтроки = СтрокиДляСогласования.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаЗаявки Из НайденныеСтроки Цикл
		
		Запись = РегистрыСведений.ЗаявкаСогласованаЦена.СоздатьМенеджерЗаписи();
		Запись.Период              = ТекущаяДата();
		Запись.ИдентификаторСтроки = СтрокаЗаявки.ИдентификаторСтроки;
		Запись.СогласованаЦена     = Истина;
		Запись.Комментарий         = СтрокаЗаявки.Комментарий;
		Запись.Ответственный       = ПараметрыСеанса.ТекущийПользователь;
		Запись.ИмяКомпьютера       = ИмяКомпьютера();
		
		Запись.Записать();
		
		РегистрыСведений.ЗаявкаСтатусы.УстановитьСледующийСтатус(СтрокаЗаявки.ИдентификаторСтроки, СтрокаЗаявки.Перечень);
		
		СтрокиДляСогласования.Удалить(СтрокаЗаявки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСогласовать(Команда)
	КомандаСогласоватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаОтказатьНаСервере()
	
	ПараметрыОтбора = Новый Структура("Выбран", Истина);
	НайденныеСтроки = СтрокиДляСогласования.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаЗаявки Из НайденныеСтроки Цикл
		
		Запись = РегистрыСведений.ЗаявкаОтмененные.СоздатьМенеджерЗаписи();
		Запись.Период              = ТекущаяДата();
		Запись.ИдентификаторСтроки = СтрокаЗаявки.ИдентификаторСтроки;
		Запись.Комментарий         = СтрокаЗаявки.Комментарий;
		Запись.Ответственный       = ПараметрыСеанса.ТекущийПользователь;
		Запись.ИмяКомпьютера       = ИмяКомпьютера();
		
		Запись.Записать();
		
		РегистрыСведений.ЗаявкаСтатусы.УстановитьСтатус(СтрокаЗаявки.ИдентификаторСтроки, Справочники.СтатусыЗаявки.СтатусОтмена());
		
		СтрокиДляСогласования.Удалить(СтрокаЗаявки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтказать(Команда)
	КомандаОтказатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаВернутьВОжиданиеНаСервере()
	
	ПараметрыОтбора = Новый Структура("Выбран", Истина);
	НайденныеСтроки = СтрокиДляСогласования.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаЗаявки Из НайденныеСтроки Цикл
		
		РегистрыСведений.ЗаявкаСтатусы.УстановитьПредыдущийСтатус(СтрокаЗаявки.ИдентификаторСтроки);
		
		СтрокиДляСогласования.Удалить(СтрокаЗаявки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВернутьВОжидание(Команда)
	
	КомандаВернутьВОжиданиеНаСервере();
	Элементы.СтрокиНеобработанные.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПользователя() Экспорт
	
	ПоказатьОповещениеПользователя("КУПОЛ", "Бла-бла-бла", "Есть заявки требующие внимания", БиблиотекаКартинок.НапоминаниеРО, СтатусОповещенияПользователя.Важное);
	
КонецПроцедуры